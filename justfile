# Name of the application's project.
name := 'sticky-notes'

# Name of the application's applet binary.
applet := 'notes-applet'


# Name of the application's service binary.
service := 'notes-service'

# The unique ID of the application.
appid := 'com.github.aae.sticky_notes'

# Path to root file system, which defaults to `/`.
rootdir := ''
# The prefix for the `/usr` directory.
prefix := '/usr'
# The location of the cargo target directory.
cargo-target-dir := env('CARGO_TARGET_DIR', 'target')

# Application's appstream metadata
appdata := appid + '.metainfo.xml'
# Application's desktop entry
desktop := appid + '.desktop'
# Application's service desktop entry to launch standalone
desktop-svc := appid + '_service' + '.desktop'
# Application's icon.
icon-svg := appid + '.svg'

# Install destinations
base-dir := absolute_path(clean(rootdir / prefix))
appdata-dst := base-dir / 'share' / 'appdata' / appdata
bin-dst-app := base-dir / 'bin' / applet
bin-dst-svc := base-dir / 'bin' / service
desktop-dst := base-dir / 'share' / 'applications' / desktop
desktop-svc-dst := base-dir / 'share' / 'applications' / desktop-svc
icons-dst := base-dir / 'share' / 'icons' / 'hicolor'
icon-svg-dst := icons-dst / 'scalable' / 'apps' / icon-svg

# Logging configuration
log-tracing := 'warn,sticky_notes=trace'

# Default recipe: check, build with debug profile, run applet (which will launch service)
default: check debug rund-applet

# Runs `cargo clean`
clean:
    cargo clean

# Removes vendored dependencies
clean-vendor:
    rm -rf .cargo vendor vendor.tar

# `cargo clean` and removes vendored dependencies
clean-dist: clean clean-vendor

# Compiles with debug profile
build-debug *args:
    cargo build {{args}}

# Compiles with release profile
build-release *args: (build-debug '--release' args)

# Compiles release profile with vendored dependencies
build-vendored *args: vendor-extract (build-release '--frozen --offline' args)

# Runs a clippy check
check *args:
    cargo clippy --tests --all-features {{args}} -- -W clippy::pedantic

# Runs a clippy check with JSON message format
check-json: (check '--message-format=json')

# Compiles all targets with release profile
release *args:
    cargo build --release --bin {{service}} --bin {{applet}} {{args}}

# Compiles all targets with debug profile
debug *args:
    cargo build --bin {{service}} --bin {{applet}} {{args}}

# Run the application for debugging purposes
rund *args:
    env RUST_BACKTRACE=full RUST_LOG={{log-tracing}} cargo run --bin {{args}}

# Run applet (debug)
rund-applet *args: (rund applet args)

# Run service (debug)
rund-service *args: (rund service args)

# Run the application for testing purposes
run *args:
    env RUST_BACKTRACE=full RUST_LOG={{log-tracing}} cargo run --release --bin {{args}}

# Run applet (release)
run-applet *args: (run applet args)

# Run service (release)
run-service *args: (run service args)

# Installs files
install:
    install -Dm0755 {{ cargo-target-dir / 'release' / applet }} {{bin-dst-app}}
    install -Dm0755 {{ cargo-target-dir / 'release' / service }} {{bin-dst-svc}}
    install -Dm0644 {{ 'resources' / desktop }} {{desktop-dst}}
    install -Dm0644 {{ 'resources' / desktop-svc }} {{desktop-svc-dst}}
    install -Dm0644 {{ 'resources' / appdata }} {{appdata-dst}}
    install -Dm0644 {{ 'resources' / 'icons' / 'hicolor' / 'scalable' / 'apps' / icon-svg }} {{icon-svg-dst}}

# Uninstalls installed files
uninstall:
    rm {{bin-dst-app}} {{bin-dst-svc}} {{desktop-dst}} {{appdata-dst}} {{icon-svg-dst}}

# Vendor dependencies locally
vendor:
    mkdir -p .cargo
    cargo vendor | head -n -1 > .cargo/config.toml
    echo 'directory = "vendor"' >> .cargo/config.toml
    tar pcf vendor.tar vendor
    rm -rf vendor

# Extracts vendored dependencies
vendor-extract:
    rm -rf vendor
    tar pxf vendor.tar

# Bump cargo version, create git commit, and create tag
tag version:
    find -type f -name Cargo.toml -exec sed -i '0,/^version/s/^version.*/version = "{{version}}"/' '{}' \; -exec git add '{}' \;
    cargo check
    cargo clean
    git add Cargo.lock
    git commit -m 'release: {{version}}'
    git commit --amend
    git tag -a {{version}} -m ''
